namespace servercont FLGrainInterfaces;
namespace serveriface FLGrainInterfaces;
namespace serverimpl FLGrains;
namespace client Network;
namespace clientcont Network.Types;

enum HandShakeMode
{
	ClientID,
	EmailAndPassword
	//?? GFGP token
}

handshake
{
	mode: HandShakeMode;
    clientID: guid?;
	email: string?;
	password: string?;
}

enum GameState
{
    New,
    WaitingForSecondPlayer,
    InProgress,
    Finished
}

enum Statistics
{
	GamesWon,
	GamesLost,
	GamesEndedInDraw,
	RoundsWon,
	RoundsLost,
	RoundsEndedInDraw,
	BestGameScore,
	BestRoundScore,
	GroupChosen_Param,
	GroupWon_Param,
	GroupLost_Param,
	GroupEndedInDraw_Param,
	WordsPlayedScore_Param,
	WordsPlayedDuplicate,
	WordsCorrected,
	RewardMoneyEarned,
	RoundWinMoneyEarned,
	MoneySpentCustomizations,
	MoneySpentTimePowerup,
	TimePowerupUsed,
	MoneySpentHelpPowerup,
	HelpPowerupUsed,
	MoneySpentGroupChange,
	GroupChangeUsed,
	MoneySpentRevealAnswers,
	RevealAnswersUsed,
	MoneySpentInfinitePlay,
	InfinitePlayUsed
}

enum LeaderBoardSubject
{
	Score,
	XP
}

enum LeaderBoardGroup
{
	All,
	Friends,
	Clan
}

enum GoldPackTag
{
	None,
	BestValue,
	BestSelling
}

enum IabPurchaseResult
{
	Success,
	AlreadyProcessed,
	Invalid,
	FailedToContactValidationService,
	UnknownError
}

enum RegistrationResult
{
	Success,
	EmailAddressInUse,
	InvalidEmailAddress,
	PasswordNotComplexEnough,
	UsernameInUse,
	AlreadyRegistered
}

enum SetEmailResult
{
	Success,
	NotRegistered,
	EmailAddressInUse,
	InvalidEmailAddress
}

enum SetPasswordResult
{
	Success,
	NotRegistered,
	PasswordNotComplexEnough
}

contract PlayerLeaderBoardInfo
{
	Name: string;
	//?? avatar
}

contract LeaderBoardEntryDTO
{
	Info: PlayerLeaderBoardInfo?;
	Rank: u64;
	Score: u64;
}

contract PlayerInfo
{
	ID: guid;
	Name: string;
	Level: u32;
	//?? avatar
}

contract StatisticValue
{
	Statistic: Statistics;
	Parameter: i32;
	Value: u64;
}

contract OwnPlayerInfo
{
	Name: string;
	Email: string?;
	XP: u32;
	Level: u32;
	NextLevelXPThreshold: u32;
	Score: u32;
	Rank: u32;
	Gold: u64;
	CurrentNumRoundsWonForReward: u32;
	NextRoundWinRewardTimeRemaining: timespan;
	InfinitePlayTimeRemaining: timespan?;
	StatisticsValues: StatisticValue[];
	IsRegistered: bool;
}

contract WordScorePairDTO castfrom(FLGameLogic.WordScorePair)
{
	Word, WordScorePair.word: string;
	Score, WordScorePair.score: u8;
}

contract GameInfo
{
    OtherPlayerInfo: PlayerInfo?;
    NumRounds: u8;
    Categories: string[];
	HaveCategoryAnswers: bool[];
    MyWordsPlayed: WordScorePairDTO[][];
    TheirWordsPlayed: WordScorePairDTO[][];
    MyTurnEndTime: datetime;
    MyTurnFirst: bool;
    NumTurnsTakenByOpponent: u8;
}

contract SimplifiedGameInfo
{
    GameID: guid;
    GameState: GameState;
    OtherPlayerName: string?;
    MyTurn: bool;
    MyScore: u8;
    TheirScore: u8;
}

contract ConfigValuesDTO castfrom(FLGrainInterfaces.Configuration.ConfigValues server oneway)
{
	NumRoundsToWinToGetReward: u8;
	RoundWinRewardInterval: timespan;
	NumGoldRewardForWinningRounds: u32;
	PriceToRefreshGroups: u32;
	RoundTimeExtension: timespan;
	RoundTimeExtensionPrice: u32;
	RevealWordPrice: u32;
	GetAnswersPrice: u32;
	MaxActiveGames: u32;
	InfinitePlayPrice: u32;
}

contract GoldPackConfigDTO castfrom(FLGrainInterfaces.Configuration.GoldPackConfig server oneway)
{
	Sku: string;
	NumGold: u32;
	Title: string;
	Tag: GoldPackTag;
}

endpoint System sys 
{
	notification NumRoundsWonForRewardUpdated rwu
	{
		totalRoundsWon: u32;
	}

	notification XPUpdated xp
	{
		totalXP: u32;
		totalLevel: u32;
	}

	notification StatisticUpdated st
	{
		stat: StatisticValue;
	}

	request GetStartupInfo st
	{
		{ }
		->
		{
			playerInfo: OwnPlayerInfo;
			configData: ConfigValuesDTO;
			goldPacks: GoldPackConfigDTO[];
		}
	}

	request TakeRewardForWinningRounds trwr via Player(clientid).TakeRewardForWinningRounds()
	{
		{ }
		->
		{
			totalGold: u64;
			timeUntilNextReward: timespan;
		}
	}

	request ActivateInfinitePlay inf via Player(clientid).ActivateInfinitePlay()
	{
		{ }
		->
		{
			success: bool;
			totalGold: u64;
			duration: timespan;
		}
	}

	request GetLeaderBoard lb
	{
		{
			subject: LeaderBoardSubject;
			group: LeaderBoardGroup;
		}
		->
		{
			entries: LeaderBoardEntryDTO[];
		}
	}

	request BuyGoldPack bgp via Player(clientid).ProcessGoldPackPurchase(sku, purchaseToken)
	{
		{
			sku: string;
			purchaseToken: string;
		}
		->
		{
			result: IabPurchaseResult;
			totalGold: u64;
		}
	}

	request SetNotificationsEnabled ne via Player(clientid).SetNotificationsEnabled(enable) { { enable: bool; } -> noresponse }

	request Login lg 
	{
		{
			email: string;
			password: string;
		}
		->
		{
			clientID: guid?;
		}
	}

	request PerformRegistration reg via Player(clientid).PerformRegistration(username, email, password)
	{
		{
			username: string;
			email: string;
			password: string;
		}
		->
		{
			result: RegistrationResult;
		}
	}

	request SetUsername unm via Player(clientid).SetUsername(username) { { username: string; } -> { result: bool; } }

	request SetEmail eml via Player(clientid).SetEmail(email) { { email: string; } -> { result: SetEmailResult; } }

	request UpdatePassword pwd via Player(clientid).UpdatePassword(password) { { password: string; } -> { result: SetPasswordResult; } }

	request SendPasswordRecoveryLink pwdrl { { email: string; } -> noresponse }

	request RegisterFcmToken fcm via Player(clientid).SetFcmToken(token) { { token: string; } -> noresponse }
}

endpoint Suggestion sg
{
	request SuggestCategory csug
	{
		{
			name: string;
			words: string[];
		}
		-> { }
	}

	request SuggestWord wsug
	{
		{
			categoryName: string;
			words: string[];
		}
		-> { }
	}
}

contract GroupInfoDTO castfrom (FLGrainInterfaces.Configuration.GroupConfig server oneway)
{
	Name: string;
	ID: u16;
}

endpoint Game gm
{
	notification OpponentJoined opj
	{
		gameID: guid;
		opponentInfo: PlayerInfo;
	}

	notification OpponentTurnEnded opr
	{
		gameID: guid;
		roundNumber: u8;
		wordsPlayed: WordScorePairDTO[]?;
	}

	notification GameEnded gend
	{
		gameID: guid;
		myScore: u32;
		theirScore: u32;
		myPlayerScore: u32;
		myPlayerRank: u32;
	}

	request NewGame new
	{
		{ }
		->
		{
			gameID: guid;
			opponentInfo: PlayerInfo;
			numRounds: u8;
			myTurnFirst: bool;
		}
	}

	request StartRound rnd via Game(gameID).StartRound(clientid)
	{
		{
			gameID: guid;
		}
		->
		{
			category: string?;
			haveAnswers: bool?;
			roundTime: timespan?;
			mustChooseGroup: bool;
			groups: GroupInfoDTO[]?;
		}
	}

	request ChooseGroup cgr via Game(gameID).ChooseGroup(clientid, groupID)
	{
		{
			gameID: guid;
			groupID: u16;
		}
		->
		{
			category: string;
			haveAnswers: bool;
			roundTime: timespan;
		}
	}

	request RefreshGroups rgr via Player(clientid).RefreshGroups(gameID)
	{
		{
			gameID: guid;
		}
		->
		{
			groups: GroupInfoDTO[];
		}
	}

	request PlayWord word via Game(gameID).PlayWord(clientid, word)
	{
		{
			gameID: guid;
			word: string;
		}
		->
		{
			wordScore: u8;
			corrected: string;
		}
	}

	request IncreaseRoundTime irt via Player(clientid).IncreaseRoundTime(gameID)
	{
		{
			gameID: guid;
		}
		->
		{
			gold: u64?;
			remainingTime: timespan?;
		}
	}

	request RevealWord rvw via Player(clientid).RevealWord(gameID)
	{
		{
			gameID: guid;
		}
		->
		{
			gold: u64?;
			word: string?;
			wordScore: u8?;
		}
	}

	request EndRound endr
	{
		{
			gameID: guid;
		}
		->
		{
			opponentWords: WordScorePairDTO[]?;
		}
	}

	request GetGameInfo info via Game(gameID).GetGameInfo(clientid)
	{
		{
			gameID: guid;
		}
		->
		{
			gameInfo: GameInfo;
		}
	}

	request GetAllGames all
	{
		{ }
		->
		{
			games: SimplifiedGameInfo[];
		}
	}

	request GetAnswers ans via Player(clientid).GetAnswers(category)
	{
		{
			category: string;
		}
		->
		{
			words: string[];
			totalGold: u64?;
		}
	}

	request Vote vote { { category: string; up: bool; } -> noresponse }
}