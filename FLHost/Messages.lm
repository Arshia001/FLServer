namespace servercont FLGrainInterfaces;
namespace serveriface FLGrainInterfaces;
namespace serverimpl FLGrains;
namespace client Network;
namespace clientcont Network.Types;

handshake { 
    clientID: guid?;
}

enum GameState
{
    New,
    WaitingForSecondPlayer,
    InProgress,
    Finished
}

enum LeaderBoardSubject
{
	Score,
	XP
}

enum LeaderBoardGroup
{
	All,
	Friends,
	Clan
}

contract LeaderBoardEntryDTO
{
	Name: string?;
	Rank: u64;
	Score: u64;
	//?? avatar
}

contract PlayerInfo
{
	ID: guid;
	Name: string;
	//?? avatar
}

contract OwnPlayerInfo
{
	Name: string;
	XP: u32;
	Level: u32;
	NextLevelXPThreshold: u32;
	Score: u32;
	Rank: u32;
	Gold: u64;
	CurrentNumRoundsWonForReward: u32;
	NextRoundWinRewardTimeRemaining: timespan;
	InfinitePlayTimeRemaining: timespan?;
}

contract WordScorePairDTO castfrom(FLGameLogic.WordScorePair)
{
	Word, WordScorePair.word: string;
	Score, WordScorePair.score: u8;
}

contract GameInfo
{
    OtherPlayerInfo: PlayerInfo?;
    NumRounds: u8;
    Categories: string[];
    MyWordsPlayed: WordScorePairDTO[][];
    TheirWordsPlayed: WordScorePairDTO[][];
    MyTurnEndTime: datetime;
    MyTurnFirst: bool;
    NumTurnsTakenByOpponent: u8;
}

contract SimplifiedGameInfo
{
    GameID: guid;
    GameState: GameState;
    OtherPlayerName: string?;
    MyTurn: bool;
    MyScore: u8;
    TheirScore: u8;
}

contract ConfigValuesDTO castfrom(ConfigValues server oneway)
{
	NumRoundsToWinToGetReward: u8;
	RoundWinRewardInterval: timespan;
	NumGoldRewardForWinningRounds: u32;
	PriceToRefreshGroups: u32;
	RoundTimeExtension: timespan;
	RoundTimeExtensionPrice: u32;
	RevealWordPrice: u32;
	MaxActiveGames: u32;
	InfinitePlayPrice: u32;
}

endpoint System sys 
{
	notification NumRoundsWonForRewardUpdated rwu
	{
		totalRoundsWon: u32;
	}

	notification XPUpdated xp
	{
		totalXP: u32;
		totalLevel: u32;
	}

	request GetStartupInfo st
	{
		{ }
		->
		{
			playerInfo: OwnPlayerInfo;
			configData: ConfigValuesDTO;
		}
	}

	request TakeRewardForWinningRounds trwr
	{
		{ }
		->
		{
			totalGold: u64;
			timeUntilNextReward: timespan;
		}
	}

	request ActivateInfinitePlay inf via Player(clientid).ActivateInfinitePlay()
	{
		{ }
		->
		{
			success: bool;
			totalGold: u64;
			duration: timespan;
		}
	}

	request GetLeaderBoard lb
	{
		{
			subject: LeaderBoardSubject;
			group: LeaderBoardGroup;
		}
		->
		{
			entries: LeaderBoardEntryDTO[];
		}
	}
}

endpoint Suggestion sg
{
	request SuggestCategory csug
	{
		{
			name: string;
			words: string[];
		}
		-> { }
	}

	request SuggestWord wsug
	{
		{
			categoryName: string;
			words: string[];
		}
		-> { }
	}
}

contract GroupInfoDTO castfrom (GroupConfig server oneway)
{
	Name: string;
	ID: u16;
}

endpoint Game gm
{
	notification OpponentJoined opj
	{
		gameID: guid;
		opponentInfo: PlayerInfo;
	}

	notification OpponentTurnEnded opr
	{
		gameID: guid;
		roundNumber: u8;
		wordsPlayed: WordScorePairDTO[]?;
	}

	notification GameEnded gend
	{
		gameID: guid;
		myScore: u32;
		theirScore: u32;
		myPlayerScore: u32;
		myPlayerRank: u32;
	}

	request NewGame new
	{
		{ }
		->
		{
			gameID: guid;
			opponentInfo: PlayerInfo;
			numRounds: u8;
			myTurnFirst: bool;
		}
	}

	request StartRound rnd via Game(gameID).StartRound(clientid)
	{
		{
			gameID: guid;
		}
		->
		{
			category: string?;
			roundTime: timespan?;
			mustChooseGroup: bool;
			groups: GroupInfoDTO[]?;
		}
	}

	request ChooseGroup cgr via Game(gameID).ChooseGroup(clientid, groupID)
	{
		{
			gameID: guid;
			groupID: u16;
		}
		->
		{
			category: string;
			roundTime: timespan;
		}
	}

	request RefreshGroups rgr via Player(clientid).RefreshGroups(gameID)
	{
		{
			gameID: guid;
		}
		->
		{
			groups: GroupInfoDTO[];
		}
	}

	request PlayWord word via Game(gameID).PlayWord(clientid, word)
	{
		{
			gameID: guid;
			word: string;
		}
		->
		{
			wordScore: u8;
			corrected: string;
		}
	}

	request IncreaseRoundTime irt via Player(clientid).IncreaseRoundTime(gameID)
	{
		{
			gameID: guid;
		}
		->
		{
			gold: u64?;
			remainingTime: timespan?;
		}
	}

	request RevealWord rvw via Player(clientid).RevealWord(gameID)
	{
		{
			gameID: guid;
		}
		->
		{
			gold: u64?;
			word: string?;
			wordScore: u8?;
		}
	}

	request EndRound endr
	{
		{
			gameID: guid;
		}
		->
		{
			opponentWords: WordScorePairDTO[]?;
		}
	}

	request GetGameInfo info
	{
		{
			gameID: guid;
		}
		->
		{
			gameInfo: GameInfo;
		}
	}

	request GetAllGames all
	{
		{ }
		->
		{
			games: SimplifiedGameInfo[];
		}
	}

	request GetAnswers ans
	{
		{
			category: string;
		}
		->
		{
			words: WordScorePairDTO[];
		}
	}

	request Vote vote { { category: string; up: bool; } -> noresponse }
}